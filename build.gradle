apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'eclipse'
apply plugin: 'idea'

version = "2.2.0-pre1"
group = "com.github.parker8283"
archivesBaseName = "BON"

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.ow2.asm:asm-debug-all:5.0.3'
    compile 'com.google.guava:guava:17.0'
    compile 'net.sf.jopt-simple:jopt-simple:4.8'
}

task copyAndReplace(type: Copy) {
    from(sourceSets.main.java) {
        include '**/ClassRemapper.java'
        expand 'DEV': project.version
    }
    from(sourceSets.main.java) {
        exclude '**/ClassRemapper.java'
    }
    into project.getBuildDir().toString() + "/sources/java"
}
tasks.compileJava.dependsOn copyAndReplace
tasks.compileJava.setSource(project.getBuildDir().toString() + '/sources/java')

def commonManifest = {
    attributes 'Main-Class': 'com.github.parker8283.bon2.BON2',
            'Implementation-Title': 'Bearded Octo Nemesis 2',
            'Implementation-Version': project.version,
            'Built-With': 'Gradle v' + project.getGradle().getGradleVersion()
}

jar {
    manifest commonManifest
    classifier = 'bin'
}

task fatJar(type: Jar, dependsOn: 'jar') {
    inputs.file jar.archivePath
    from(configurations.compile.collect { zipTree(it).matching { exclude 'META-INF/**' } })
    manifest commonManifest
    classifier = 'all'
}

fatJar.doFirst {
    from zipTree(jar.archivePath)
}

artifacts {
    archives fatJar
}
